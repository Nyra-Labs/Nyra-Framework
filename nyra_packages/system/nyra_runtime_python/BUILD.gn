#
# Copyright Â© 2024 Agora
# This file is part of NYRA Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/nyra_package.gni")

config("nyra_runtime_python_common_libs") {
  if (is_win) {
    libs = [ "nyra_runtime_python.dll.lib" ]
  } else {
    libs = [ "nyra_runtime_python" ]
  }
}

config("nyra_runtime_python_common_config") {
  configs = [ ":nyra_runtime_python_common_libs" ]

  include_dirs = [ "//nyra_packages/system/nyra_runtime_python/include" ]
  lib_dirs = [ "//nyra_packages/system/nyra_runtime_python/lib" ]
}

config("config_for_app") {
  configs = [ ":nyra_runtime_python_common_config" ]

  if (is_linux) {
    ldflags =
        [ "-Wl,-rpath=\$ORIGIN/../nyra_packages/system/nyra_runtime_python/lib" ]
  } else if (is_mac) {
    ldflags = [
      "-Wl,-rpath,@loader_path/../nyra_packages/system/nyra_runtime_python/lib",
    ]
  }
}

config("config_for_nyra_packages") {
  configs = [ ":nyra_runtime_python_common_config" ]

  if (is_linux) {
    ldflags = [ "-Wl,-rpath=\$ORIGIN/../../../system/nyra_runtime_python/lib" ]
  } else if (is_mac) {
    ldflags =
        [ "-Wl,-rpath,@loader_path/../../../system/nyra_runtime_python/lib" ]
  }
}

config("config_for_standalone_nyra_packages") {
  configs = [ ":nyra_runtime_python_common_libs" ]

  include_dirs = [ "//.ten/app/nyra_packages/system/nyra_runtime_python/include" ]
  lib_dirs = [ "//.ten/app/nyra_packages/system/nyra_runtime_python/lib" ]
}

nyra_package("nyra_runtime_python") {
  package_kind = "system"

  resources = [
    "interface",
    "lib",
    "manifest.json",
    "tools",
  ]
}
